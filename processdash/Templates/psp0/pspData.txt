/* -*- mode:c -*-
=============================================================================
== Process Dashboard - Data Automation Tool for high-maturity processes
== Copyright (C) 1998-2008 Tuma Solutions, LLC
== 
== This program is free software; you can redistribute it and/or
== modify it under the terms of the GNU General Public License
== as published by the Free Software Foundation; either version 2
== of the License, or (at your option) any later version.
== 
== This program is distributed in the hope that it will be useful,
== but WITHOUT ANY WARRANTY; without even the implied warranty of
== MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
== GNU General Public License for more details.
== 
== You should have received a copy of the GNU General Public License
== along with this program; if not, write to the Free Software
== Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
== 
== The author(s) may be contacted at:
== Process Dashboard Group
== c/o Ken Raisor
== 6137 Wardleigh Road
== Hill AFB, UT 84056-5843
== 
== E-Mail POC:  processdash-devel@lists.sourceforge.net

=============================================================================
== Author:	Dave Tuma -TISHD- <dtuma@ulaf1>
== Created:	Thu Jan  8 16:34:18 1998
==
== Description: This is the data file for a PSP0 process. 
==
*/

/*
 * The following element identifies the data in this file as belonging to
 * a PSP process.
 */

[PSP Project] == TAG;
[Has Compile Phase] = TAG;


/*
 * Define the default size metric for PSP - LOC.
 */

[SIZE_METRIC_NAME] == "New & Changed LOC";
[SIZE_METRIC_NAME_ABBR] == "LOC";
[AGGR_SIZE_METRIC_NAME] == "New & Changed KLOC";
[AGGR_SIZE_METRIC_NAME_ABBR] == "KLOC";
[PROBE_INPUT_SIZE_METRIC_NAME] == "Estimated Object LOC";
[PROBE_NO_EDIT_INPUT] == TAG;

[Size] = [New & Changed LOC];
[Estimated Size] = [Estimated New & Changed LOC];

[Aggregate Size] = [New & Changed KLOC];
[Estimated Aggregate Size] = [Estimated New & Changed KLOC];

[New & Changed KLOC] =  [New & Changed LOC] / 1000;
[Estimated New & Changed KLOC] = [Estimated New & Changed LOC] / 1000;

[Base LOC]     = 0;
[Deleted LOC]  = 0;
[Modified LOC] = 0;
[Reused LOC]   = 0;
[Total LOC]    = 0;
[New Reused LOC] = 0;

[New & Changed LOC] = [Added LOC] + [Modified LOC];
[Added LOC] = [Total LOC] - [Base LOC] + [Deleted LOC] - [Reused LOC];


/*
 * Defect Injection/Removal by phase metrics
 */

[Design/Cumulative Defect Injection %] =
    sumFor("Defects Injected", ",Planning,HLD,HLD Review,Design,") /
    [Defects Injected];
[Code/Cumulative Defect Injection %] =
    [Design/Cumulative Defect Injection %] +
    sumFor("Defects Injected", ",Design Review,Code,") / [Defects Injected];
[Design Review/Cumulative Defect Removal %] =
    sumFor("Defects Removed", ",Planning,HLD,HLD Review,Design,Design Review,")
    / [Defects Removed];
[Code Review/Cumulative Defect Removal %] =
    [Design Review/Cumulative Defect Removal %] +
    sumFor("Defects Removed", ",Code,Code Review,") / [Defects Removed];
[Compile/Cumulative Defect Removal %] =
    [Code Review/Cumulative Defect Removal %] +
    [Compile/Defects Removed] / [Defects Removed];
[Test/Cumulative Defect Removal %] =
    [Compile/Cumulative Defect Removal %] +
    [Test/Defects Removed] / [Defects Removed];

/*
 * Calculations for the quality profile
 */

[Quality Profile Capable] = tag;

[Design vs Code/Time Ratio] = [Design/Time] / [Code/Time];
[Design vs Code/Estimated Time Ratio] = 
    [Design/Estimated Time] / [Code/Estimated Time];

[Quality_Profile_1] = iff([Code/Time], 
    [Design/Time] / ([Code/Time] * inherit("Target Design/Code Ratio")), 1);

[Code Review vs Code/Time Ratio] = [Code Review/Time] / [Code/Time];
[Code Review vs Code/Estimated Time Ratio] =
    [Code Review/Estimated Time] / [Code/Estimated Time];

[Quality_Profile_2] = iff([Code/Time],
    [Code Review/Time] / ([Code/Time] *
			  inherit("Target Code Review/Code Ratio")), 1);

[Quality_Profile_3] = iff([Compile/Defects Removed] and [Size], 
    2 / (1 + [Compile/Defect Density] /
	 inherit("Target Compile Defect Density")), 1);

[Quality_Profile_4] = iff([Test/Defects Removed] and [Size],
    2 / (1 + [Test/Defect Density] /
	 inherit("Target Test Defect Density")), 1);

[Design Review vs Design/Time Ratio] = [Design Review/Time] / [Design/Time];
[Design Review vs Design/Estimated Time Ratio] =
    [Design Review/Estimated Time] / [Design/Estimated Time];

[Quality_Profile_5] = iff([Design/Time], 
    [Design Review/Time]  / ([Design/Time] *
			     inherit("Target Design Review/Design Ratio")), 1);


// Additional ratios that might be useful for quality analysis

[HLD Review vs HLD/Time Ratio] = [HLD Review/Time] / [HLD/Time];
[HLD Review vs HLD/Estimated Time Ratio] =
    [HLD Review/Estimated Time] / [HLD/Estimated Time];

[HLD vs Design/Time Ratio] = [HLD/Time] / [Design/Time];
[HLD vs Design/Estimated Time Ratio] = 
    [HLD/Estimated Time] / [Design/Estimated Time];


/*
 * Design and Code Review Yields
 */

[Design Review/% Phase Yield] = min(1.0, [Design Review/Defects Removed] /
    (sumFor("Defects Injected", 
            ",Planning,HLD,HLD Review,Design,Design Review,") -
     sumFor("Defects Removed", ",Planning,HLD,HLD Review,Design,")));

[Code Review/% Phase Yield] = min(1.0, [Code Review/Defects Removed] /
    (sumFor("Defects Injected", 
            ",Planning,HLD,HLD Review,Design,Design Review,Code,Code Review,")
     - sumFor("Defects Removed",
	      ",Planning,HLD,HLD Review,Design,Design Review,Code,")));
