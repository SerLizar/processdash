// <!--#echo var="-*- mode:html -*-" defaultEncoding="data" -->
// Data file for a rollup dataset for <!--#echo Full_Name -->
//

#include <ROLLUP:PID>;
undefine([Historical Data Tag]);
[Exclude Children of Rollup_List for Defects] = tag;

/*
 * We need to redefine the rollup project list to include PSP projects.
 */
[/PSP/Project List] = search("/", "PSP Rollup Eligible");
[/PID/Indiv Root List] = search("/", "PID Indiv Root Tag");
[/PID/Full Project List] = setUnion([/PID/Project List], [/PSP/Project List]);
[Rollup_List] = filter(eval([Rollup_Filter], [_]), [/PID/Full Project List]);


/*
 * Define a rollup filter that is appropriate for data rollup instances.
 * Team and master project datafiles will override this definition.
 */
[Rollup_WBS_ID_Filter] = "," & inherit("Project_ID") & ",";
[Rollup_Filter_Regexp] =
    "m\n^(\\Q" & join("\\E|\\Q", [Rollup_WBS_ID_Filter]) & "\\E)(/.+)?$\n";
[Rollup_Filter] = "match([{Rollup_Filter_Regexp}], [Project_WBS_ID])";


/*
 * Count the number of distinct software projects in this rollup list.
 */
[Software_Project_List] =
    filter(indirect([_] &/ "New & Changed LOC") > 0, [Rollup_List]);
[Software_Project_Count] = count([Software_Project_List]);


/*
 * Calculate % Defect Free metrics for each failure phase.
 */

<!--#foreach name=Phase list=Phase_List #-->
<!--#if Phase_Is_Failure #-->

[<!--#echo Phase_Name -->/% Defect Free] = count(filter(
    !(indirect([_] &/ "<!--#echo Phase_Name -->/Defects Removed") > 0),
    [Software_Project_List])) / [Software_Project_Count];

<!--#endif--><!--#endfor-->


/*
 * Roll up phase data both from both new style (namespaced) and old style
 * (plain/unadorned) data exports.
 */
[Rollup_Unadorned_Phase_Metrics] = true;
[Rollup_Namespaced_Phase_Metrics] = true;

<!--#foreach name=PHASE list=Phase_List #-->
<!--#foreach name=METRIC values=',Estimated Time,Time,Defects Injected,Defects Removed,' #-->

[<!--#echo PHASE_Name -->/METRIC] =
    iff([Rollup_Unadorned_Phase_Metrics],
        sumFor("<!--#echo PHASE_Name -->/METRIC", [Rollup_List]), 0) +
    iff([Rollup_Namespaced_Phase_Metrics],
        sumFor("PID /<!--#echo PHASE_Name -->/METRIC", [Rollup_List])
        <!--#if PHASE_Is_PSP #-->
	+ sumFor("PSP /<!--#echo PHASE_Name -->/METRIC", [Rollup_List])
	<!--#endif-->, 0);

<!--#endfor--><!--#endfor-->



/*
 * Add up metrics for uncategorized tasks
 */

[Uncategorized/Time] = sumFor("PID /Uncategorized/Time", [Rollup_List]);
[Uncategorized/Estimated Time] =
    sumFor("PID /Uncategorized/Estimated Time", [Rollup_List]);

[Time] = sumFor("Time", [Phase_List], "Uncategorized");
[Estimated Time] = sumFor("Estimated Time", [Phase_List], "Uncategorized");
